
/* Black Forest for Forgotten Empires */

#include_drs random_map.def 54000

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement 
	ai_info_map_type BLACK_FOREST 0 0 0


/* ****************************************************** */

start_random
  percent_chance 15
  #define FROZEN_MAP
  percent_chance 15
  #define TROPICAL_MAP
end_random

<LAND_GENERATION>
if FROZEN_MAP
base_terrain SNOW_FOREST
elseif TROPICAL_MAP
base_terrain JUNGLE
else
	base_terrain FOREST
endif

create_player_lands 
{
if FROZEN_MAP
  terrain_type				SNOW
else            
  terrain_type                     GRASS
endif
  land_percent                     50
  base_size                        13
  other_zone_avoidance_distance    6
}

/* ****************************************************** */
<TERRAIN_GENERATION>

if TROPICAL_MAP
create_terrain JUNGLE
elseif FROZEN_MAP
create_terrain SNOW_FOREST
else
create_terrain FOREST
endif
{
if FROZEN_MAP
  base_terrain			   SNOW
else
  base_terrain                   GRASS
endif
  spacing_to_other_terrain_types 5
  land_percent                   20
  number_of_clumps               24
  set_avoid_player_start_areas     
}

if FROZEN_MAP
   create_terrain SNOW
else
   create_terrain GRASS
endif
{
if FROZEN_MAP
   base_terrain SNOW_FOREST
elseif TROPICAL_MAP
  base_terrain                   JUNGLE
else
   base_terrain FOREST
endif
  spacing_to_other_terrain_types 1
  land_percent                   7
  number_of_clumps               9
  set_avoid_player_start_areas     
}

create_terrain LEAVES
{
if FROZEN_MAP
   base_terrain 			SNOW
else
  base_terrain                   GRASS
endif
  spacing_to_other_terrain_types 1
  land_percent                   3
  number_of_clumps               26
}

create_terrain LEAVES
{
if FROZEN_MAP
   base_terrain 			SNOW
else
  base_terrain                   GRASS
endif
  spacing_to_other_terrain_types 1
  land_percent                   1
  number_of_clumps               40
}

if FROZEN_MAP
   create_terrain GRASS_SNOW
else
   create_terrain GRASS3
endif
{
  if FROZEN_MAP
   base_terrain 			SNOW
else
  base_terrain                   GRASS
endif
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}

/* PONDS FOR DA FISHIES */

if FROZEN_MAP
   create_terrain ICE
else
   create_terrain WATER
endif
{
if FROZEN_MAP
   base_terrain 			SNOW
else
  base_terrain                   GRASS
endif
  spacing_to_other_terrain_types 1
  land_percent                   1
  number_of_clumps               4
  set_avoid_player_start_areas
  set_flat_terrain_only     
}

create_terrain MED_WATER
{
  base_terrain                   WATER
  spacing_to_other_terrain_types 2
  land_percent                   1
  number_of_clumps               4
  set_avoid_player_start_areas
  set_flat_terrain_only     
}

/* ****************************************************** */
<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if LUDIKRIS_MAP
create_object RELIC
{
   number_of_objects            250
   min_distance_to_players     25
   temp_min_distance_group_placement 110
}
else
create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
endif
 
/* SPECIAL STUFF FOR REGICIDE */

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

if TROPICAL_MAP
create_object TURKEY
else
create_object SHEEP
endif
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    10
   max_distance_to_players    12
}
if TROPICAL_MAP
create_object TURKEY
else
create_object SHEEP
endif
{
   number_of_objects 2
   number_of_groups  2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects          4
percent_chance 50
  number_of_objects          3
end_random
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players    14
  max_distance_to_players    22
percent_chance 50
  min_distance_to_players    23
  max_distance_to_players    30
end_random
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

if TROPICAL_MAP
create_object JAGUAR
else
create_object WOLF
endif
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}

/* *****RESOURCES BY MAP***** */

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object FORAGE
{
  number_of_groups           12    
  number_of_objects          5
  group_placement_radius     3
  min_distance_to_players       40
  set_tight_grouping
  set_gaia_object_only
  temp_min_distance_group_placement  15
}
endif


/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object GOLD
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object STONE
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

if TROPICAL_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif FROZEN_MAP
create_object SNOWPINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object SNOWPINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
else
create_object OAKTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
endif

if TROPICAL_MAP
create_object MACAW
else
create_object HAWK
endif
{
   number_of_objects         6
   set_scaling_to_map_size
}

if TROPICAL_MAP
create_object JAGUAR
else
create_object WOLF
endif
{
  number_of_groups          3
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 


if TROPICAL_MAP
   create_object JAVELINA
else
   create_object BOAR
endif
{
   number_of_objects 3
   group_variance    1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

if TROPICAL_MAP
   create_object JUNGLETREE
elseif FROZEN_MAP
   create_object SNOWPINETREE
else
   create_object OAKTREE
endif
{
  number_of_objects          100
  set_gaia_object_only
  min_distance_to_players       6
  min_distance_group_placement  2
}

create_object SHORE_FISH
{
  number_of_objects          3
  set_gaia_object_only
  set_scaling_to_map_size
}

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        4
{
if FROZEN_MAP
  base_terrain			SNOW
else
  base_terrain 			GRASS
endif
  number_of_clumps 		10
  number_of_tiles 		700
  set_scale_by_groups           
}

<ELEVATION_GENERATION>
create_elevation        7
{
if FROZEN_MAP
  base_terrain			GRASS_SNOW
elseif TROPICAL_MAP 
  base_terrain 			JUNGLE
else
  base_terrain			FOREST
endif
  number_of_clumps 		7
  number_of_tiles               4300
  set_scale_by_groups           
}

/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land 
{
  replace_terrain GRASS         GRASS
  replace_terrain GRASS2        GRASS2
  replace_terrain GRASS3        GRASS3
  replace_terrain LEAVES	  LEAVES
  replace_terrain SNOW          SNOW
  replace_terrain GRASS_SNOW    GRASS_SNOW
  replace_terrain WATER         SHALLOW
  replace_terrain DEEP_WATER    SHALLOW
  replace_terrain JUNGLE        GRASS
  replace_terrain SNOW_FOREST   SNOW
  replace_terrain FOREST        GRASS 
  terrain_cost    WATER         5
  terrain_cost    MED_WATER     7
  terrain_cost    ICE		  5
  terrain_cost    JUNGLE        2
  terrain_cost    FOREST        2
  terrain_cost    SNOW_FOREST   2
  terrain_cost    GRASS         1
  terrain_cost    GRASS2        1
  terrain_cost    GRASS3        1
  terrain_cost    LEAVES        2
  terrain_cost    SNOW          1
  terrain_cost    GRASS_SNOW    1
  terrain_cost    BEACH         4
  terrain_size    WATER         2       1
  terrain_size    ICE           2       1
  terrain_size    MED_WATER     2       1
  terrain_size    GRASS         0       0
  terrain_size    LEAVES        0       0
  terrain_size    GRASS2        0       0
  terrain_size    GRASS3        0       0
  terrain_size    SNOW  	  0       0
  terrain_size    GRASS_SNOW    0       0
  terrain_size    JUNGLE        3       1
  terrain_size    FOREST        3       1
  terrain_size    SNOW_FOREST   3       1
  terrain_size    DESERT        0       0
  terrain_size    ROAD2         0       0
}

#const ROAD_SNOWY 38

create_connect_teams_lands
{
if FROZEN_MAP
  replace_terrain GRASS         ROAD_SNOWY 
  replace_terrain GRASS3        ROAD_SNOWY 
  replace_terrain GRASS2        ROAD_SNOWY 
  replace_terrain LEAVES        ROAD_SNOWY 
  replace_terrain SNOW          ROAD_SNOWY 
  replace_terrain GRASS_SNOW    ROAD_SNOWY 
  replace_terrain WATER         SHALLOW
  replace_terrain MED_WATER     SHALLOW 
  replace_terrain JUNGLE        ROAD_SNOWY 
  replace_terrain SNOW_FOREST   ROAD_SNOWY 
  replace_terrain FOREST        ROAD_SNOWY 
  replace_terrain PALM_DESERT   DESERT
else
  replace_terrain GRASS         ROAD2
  replace_terrain GRASS3        ROAD2
  replace_terrain GRASS2        ROAD2
  replace_terrain LEAVES        ROAD2
  replace_terrain SNOW          ROAD2
  replace_terrain GRASS_SNOW    ROAD2
  replace_terrain WATER         SHALLOW
  replace_terrain MED_WATER     SHALLOW 
  replace_terrain JUNGLE        ROAD2
  replace_terrain SNOW_FOREST   ROAD2 
  replace_terrain FOREST        ROAD2 
  replace_terrain PALM_DESERT   DESERT
endif
  terrain_cost    WATER         7
  terrain_cost    MED_WATER     7 
  terrain_cost    ICE           7
  terrain_cost    JUNGLE        7
  terrain_cost    FOREST        7
  terrain_cost    SNOW_FOREST   7
  terrain_cost    GRASS         2
  terrain_cost    GRASS2        2
  terrain_cost    GRASS3        2
  terrain_cost    LEAVES        2
  terrain_cost    SNOW          2
  terrain_cost    GRASS_SNOW    2
  terrain_cost    BEACH         4
  terrain_cost    ROAD2         1
  terrain_size    WATER         2       1
  terrain_size    MED_WATER     2       1
  terrain_size    ICE           2       1
  terrain_size    GRASS         1       0
  terrain_size    SNOW          1       0
  terrain_size    GRASS_SNOW    1       0
  terrain_size    GRASS2        1       0
  terrain_size    GRASS3        1       0
  terrain_size    LEAVES        1       0
  terrain_size    JUNGLE        1       0
  terrain_size    FOREST        1       0
  terrain_size    SNOW_FOREST   1       0
  terrain_size    DESERT        0       0
  terrain_size    ROAD2         0       0
}

