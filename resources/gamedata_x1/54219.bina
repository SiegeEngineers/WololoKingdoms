
/* Scandinavia for Forgotten Empires */

#include_drs random_map.def 54000

<PLAYER_SETUP>
  random_placement 
	ai_info_map_type SCANDANAVIA 0 0 0

<LAND_GENERATION>

#define FROZEN_MAP

base_terrain WATER

create_player_lands 
{ 
  terrain_type                     SNOW
  land_percent                       80
  base_size                          15
  border_fuzziness                   5
start_random
  percent_chance 50
    left_border                     12
    right_border                    12
  percent_chance 50
    top_border                      12
    bottom_border                   12
end_random
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* MIXING WATER */

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               10
spacing_to_other_terrain_types 1
land_percent 10
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               8
spacing_to_other_terrain_types 3
land_percent 5
}

create_terrain MED_WATER
{
base_terrain DEEP_WATER
number_of_clumps               5
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain WATER
{
base_terrain MED_WATER
number_of_clumps               5
spacing_to_other_terrain_types 1
land_percent 10
}

create_terrain ICE 
{
base_terrain WATER
land_percent                     2
number_of_clumps                 10
spacing_to_other_terrain_types   1
set_flat_terrain_only     
}


/* FROZEN LAKES */

create_terrain ICE 
{
base_terrain SNOW
land_percent                     4
number_of_clumps                 8
spacing_to_other_terrain_types   1
set_avoid_player_start_areas
set_flat_terrain_only     
}

create_terrain ICE 
{
base_terrain SNOW
land_percent                     1
number_of_clumps                 6
spacing_to_other_terrain_types   1
set_avoid_player_start_areas
set_flat_terrain_only     
}

start_random
percent_chance 50
create_terrain ICE 
{
base_terrain SNOW
land_percent                     1
number_of_clumps                 6
spacing_to_other_terrain_types   1
set_avoid_player_start_areas
set_flat_terrain_only     
}
end_random


/* PRIMARY FOREST */

create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   12
  number_of_clumps               8
  set_avoid_player_start_areas     
  set_scale_by_groups
}

create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   5
  number_of_clumps               20
  set_avoid_player_start_areas     
  set_scale_by_groups
}

/* PRIMARY PATCH */

create_terrain GRASS_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               20
  spacing_to_other_terrain_types 1
  land_percent                   9
  set_scale_by_size
}

/* SECONDARY PATCH */

create_terrain DIRT_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}

/* TERTIARY PATCH */

create_terrain GRASS2
{
  base_terrain                   GRASS_SNOW
  number_of_clumps               10
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if LUDIKRIS_MAP
create_object RELIC
{
   number_of_objects            250
   min_distance_to_players     25
   temp_min_distance_group_placement 110
}
else
create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
endif


/* SPECIAL STUFF FOR REGICIDE */

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* BAA BAA */

create_object SHEEP
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    9
   max_distance_to_players    11
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups  2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects          8
percent_chance 50
  number_of_objects          7
end_random
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players    14
  max_distance_to_players    22
percent_chance 50
  min_distance_to_players    23
  max_distance_to_players    30
end_random
}

create_object BOAR
{
  number_of_objects          2
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object BOAR
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object WOLF
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}

/* *****RESOURCES BY MAP***** */

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          5
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          5
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          6
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           6    
  number_of_objects          6
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object GOLD
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object STONE
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

create_object SNOWPINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object SNOWPINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}

create_object HAWK
{
   number_of_objects         4
   set_scaling_to_map_size
}

create_object WOLF
{
  number_of_groups          2
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 

create_object DEER
{
   number_of_objects 3
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    19
   man_distance_to_players    25
}

create_object SNOWPINETREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}

/* CAPTAIN FIN BOSS */

create_object DORADO
{
  number_of_objects                   6
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
  min_distance_group_placement  4
}
create_object TUNA
{
  number_of_objects                   150
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
  min_distance_group_placement  8
}

create_object SHORE_FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  set_gaia_object_only
 min_distance_group_placement  3
}

/* CAPTAIN FIN BOSS END */

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
  base_terrain             SNOW  
  number_of_clumps 		20
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size           
}

/* ****************************************************** */
<CLIFF_GENERATION>
min_number_of_cliffs 5
max_number_of_cliffs 10
min_length_of_cliff  3
max_length_of_cliff  7

