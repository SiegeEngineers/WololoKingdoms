/* ************ TEAM ISLANDS ************ */
/* 21 DEC 99 */

#include_drs random_map.def 54000
#const BURMESE_REVEALER  		1378
#const RELIC_TERRAIN  		27

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>
base_terrain WATER

start_random
  percent_chance 20
  #define DESERT_MAP
  percent_chance 20
  #define ALPINE_MAP
  percent_chance 20
  #define FROZEN_MAP
end_random

create_player_lands 
{            
if DESERT_MAP
  terrain_type                     DIRT
elseif ALPINE_MAP
  terrain_type                     GRASS2
elseif FROZEN_MAP
  terrain_type                     SNOW
else           
  terrain_type                     GRASS
endif
land_percent                     45
base_size                        12
border_fuzziness                 15
start_random
   percent_chance                  50
      left_border                   7
      right_border                  9
   percent_chance                  50
	left_border                   9
      right_border                  7
end_random
start_random
   percent_chance                  50
      top_border                    7
      bottom_border                 9
   percent_chance                  50
	top_border                    9
      bottom_border                 7
end_random
  set_zone_by_team
  other_zone_avoidance_distance    22
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* MIXING WATER */

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               10
spacing_to_other_terrain_types 2
land_percent 40
}

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               8
spacing_to_other_terrain_types 3
land_percent 20
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain MED_WATER
{
base_terrain DEEP_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

create_terrain WATER
{
base_terrain MED_WATER
number_of_clumps               30
spacing_to_other_terrain_types 1
land_percent 1
}

/* PRIMARY FOREST */

if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 5
  land_percent                   6
  number_of_clumps               8
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               8
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               8
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               8
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if DESERT_MAP
   create_terrain DESERT
{
  base_terrain                   DIRT
  number_of_clumps               10
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               16
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain GRASS_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               16
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
else
create_terrain DIRT
{
  base_terrain                   GRASS
  number_of_clumps               10
  spacing_to_other_terrain_types 1
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if DESERT_MAP
create_terrain FOREST
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain FOREST
{
  base_terrain                   GRASS3
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS_SNOW
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if DESERT_MAP
   create_terrain DIRT3
{
  base_terrain                   DIRT
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain DIRT_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if DESERT_MAP
   create_terrain GRASS3
{
  base_terrain                   DIRT
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain GRASS2
{
  base_terrain                   GRASS_SNOW
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

create_terrain RELIC_TERRAIN
{
	number_of_tiles 1
	set_avoid_player_start_areas
	number_of_clumps 5
	if DESERT_MAP
 	   base_terrain        		DIRT3
	elseif ALPINE_MAP
 	   base_terrain        		GRASS
	elseif FROZEN_MAP
		base_terrain        	GRASS2
	else           
	   base_terrain        		GRASS3
	endif
}

/* ****************************************************** */
<OBJECTS_GENERATION>

#include_drs land_and_water_resources.inc 54102

create_object RELIC 
{
	number_of_objects 5
	terrain_to_place_on RELIC_TERRAIN
	set_gaia_object_only 
}

create_object BURMESE_REVEALER
{
	number_of_objects 10
	terrain_to_place_on RELIC_TERRAIN 
	set_place_for_every_player
}
/* PLAYER START OBJECTS */

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }
create_object DEER
{
   number_of_objects 6
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

create_object BOAR
{
   number_of_objects 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects          20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif ALPINE_MAP
create_object PINETREE
{
  number_of_objects          20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif FROZEN_MAP
create_object SNOWPINETREE
{
  number_of_objects          20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif

if DESERT_MAP
create_object DORADO
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
elseif FROZEN_MAP
create_object SALMON
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
elseif ALPINE_MAP
create_object SALMON
{
  number_of_objects                   15
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
else
create_object DORADO
{
  number_of_objects                   5
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object TUNA
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}
create_object SNAPPER
{
  number_of_objects                   10
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
} 
endif

create_object SHORE_FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  min_distance_group_placement         3
  set_gaia_object_only
}

create_object MARLIN1
{
  number_of_groups                      5
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10 
  max_distance_to_other_zones         7
} 

create_object MARLIN2
{
  number_of_groups                      5
  number_of_objects                     1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement        10 
  max_distance_to_other_zones         7
} 


/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
if DESERT_MAP
  base_terrain                DIRT
elseif ALPINE_MAP
  base_terrain                GRASS2
elseif FROZEN_MAP
  base_terrain                SNOW
else           
  base_terrain                GRASS
endif
  number_of_clumps 		14
  number_of_tiles 		1200
  set_scale_by_groups
  set_scale_by_size           
}

<CLIFF_GENERATION>

min_number_of_cliffs 3
max_number_of_cliffs 5
min_length_of_cliff  4
max_length_of_cliff  8
cliff_curliness      10
min_distance_cliffs  3     


/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_teams_lands
{
  replace_terrain GRASS         ROAD2
  replace_terrain GRASS2        ROAD2
  replace_terrain GRASS3        ROAD2
  replace_terrain GRASS_SNOW    ROAD2
  replace_terrain DIRT_SNOW     ROAD2
  replace_terrain SNOW          ROAD2
  replace_terrain WATER         GRASS
  replace_terrain MED_WATER     GRASS
  replace_terrain DEEP_WATER    GRASS
  replace_terrain BEACH         GRASS
  replace_terrain FOREST        ROAD2
  replace_terrain PALM_DESERT   ROAD2
  replace_terrain PINE_FOREST   ROAD2
  replace_terrain SNOW_FOREST   ROAD2
  replace_terrain DESERT        ROAD2
  replace_terrain DIRT          ROAD2
  replace_terrain DIRT2         ROAD2
  replace_terrain DIRT3         ROAD2
  terrain_cost    WATER         5
  terrain_cost    MED_WATER     7
  terrain_cost    DEEP_WATER    15
  terrain_cost    FOREST        7
  terrain_cost    PALM_DESERT   7
  terrain_cost    PINE_FOREST   7  
  terrain_cost    SNOW_FOREST   7  
  terrain_cost    DESERT        5
  terrain_cost    DIRT          5
  terrain_cost    DIRT2         5
  terrain_cost    DIRT3         5
  terrain_cost    DIRT_SNOW     5
  terrain_cost    GRASS         2
  terrain_cost    SNOW          2
  terrain_cost    GRASS2        2
  terrain_cost    GRASS3        2
  terrain_cost    GRASS_SNOW    2
  terrain_cost    BEACH         4
  terrain_cost    ROAD2         1
  terrain_size    WATER         6       1
  terrain_size    DEEP_WATER    6       1
  terrain_size    GRASS         1       0
  terrain_size    GRASS2        1       0
  terrain_size    GRASS3        1       0
  terrain_size    SNOW          1       0
  terrain_size    GRASS_SNOW    1       0
  terrain_size    FOREST        3       1
  terrain_size    PALM_DESERT   3       1
  terrain_size    PINE_FOREST   3       1
  terrain_size    SNOW_FOREST   3       1
  terrain_size    DESERT        0       0
  terrain_size    DIRT          0       0
  terrain_size    DIRT2         0       0
  terrain_size    DIRT3         0       0
  terrain_size    DIRT_SNOW     0       0
  terrain_size    ROAD2         0       0
}
